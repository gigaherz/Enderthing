import java.util.concurrent.Callable

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.134'
    id 'com.github.johnrengelman.shadow' version '8.1+'
}

version = '1.5.1'
group = 'dev.gigaherz.enderthing'

Provider<String> minecraftVersion(Transformer<String, String> mapper) {
    return project.provider((() -> project.userDevRuntime.definitions.values().first().specification.minecraftVersion))
            .map(mapper)
}

base {
    archivesName = minecraftVersion { "Enderthing-${it}" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

repositories {
    mavenLocal()
    maven {
        url 'https://dogforce-games.com/maven'
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'https://maven.mcmoddev.com/'
    }
    maven {
        url "https://github.com/TehNut/temporary-maven-thing/tree/master/maven"
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'forge.logging.markers', '' // 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        workingDirectory project.file('run')

        modSource project.sourceSets.main
    }

    client {
    }

    server {
    }

    data {
        programArguments.addAll '--mod', 'enderthing', '--all', '--output', file('src/main/generated_resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

subsystems {
    parchment {
        minecraftVersion="1.20.6"
        mappingsVersion="2024.05.01"
    }
}

dependencies {
    implementation 'net.neoforged:neoforge:20.6.75-beta'

    /*compileOnly fg.deobf("mezz.jei:jei-1.19-common-api:11.0.0.206")
    runtimeOnly fg.deobf("mezz.jei:jei-1.19-common:11.0.0.206")
    compileOnly fg.deobf("mezz.jei:jei-1.19-forge-api:11.0.0.206")
    runtimeOnly fg.deobf("mezz.jei:jei-1.19-forge:11.0.0.206")*/
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
            "Specification-Title": "enderthing",
            "Specification-Vendor": "gigaherz",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"gigaherz",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}